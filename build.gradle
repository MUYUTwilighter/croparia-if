plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4.162'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url "<https://maven.shedaniel.me/>" }
        maven { url "<https://maven.architectury.dev/>" }
        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/"
        }
        maven {
            // saps.dev Maven (KubeJS and Rhino)
            url = "https://maven.saps.dev/minecraft"
            content {
                includeGroup "dev.latvian.mods"
            }
        }
        maven {
            url "https://jitpack.io"
            content {
                includeGroup "com.github.llamalad7.mixinextras"
            }
        }
        maven {
            url "https://cursemaven.com"
        }
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            // location of the maven that hosts JEI files since January 2023
            name = "Jared's maven"
            url = "https://maven.blamejared.com/"
        }
        maven {
            // location of a maven mirror for JEI files, as a fallback
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
        if (getRootDir().toPath().resolve("flat").toFile().exists()) {
            flatDir {
                dir(getRootDir().toPath().resolve("flat"))
            }
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-$minecraft_version:$parchment_version@zip")
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            maven {
                name = "MuyuMaven"
                url = uri("https://maven.muyucloud.cool/repository/maven-releases/")
                credentials {
                    username = System.getenv("MUYU_MAVEN_USERNAME") ?: ""
                    password = System.getenv("MUYU_MAVEN_PASSWORD") ?: ""
                }
            }
        }
    }
}

def findTaskPath = { String projPath, String taskName ->
    def p = project(projPath)
    def t = p.tasks.named(taskName).get()
    return t != null ? "${projPath}:${taskName}" : null
}

tasks.register("publishAll") {
    group = "publishing"
    description = "Publishes all platforms to their configured services (Modrinth, CurseForge, etc)."

    def deps = []

    enabled_platforms.split(",").collect { p -> ":" + p.trim() }.forEach {p -> {
        ['curseforge', 'modrinth'].forEach { taskName ->
            def t = findTaskPath(p, taskName)
            if (t != null) {
                deps.add(t)
            }
        }
    }}

    dependsOn deps
}