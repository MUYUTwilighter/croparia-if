import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'com.gradleup.shadow'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.26'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    // Tomlj
    include("org.tomlj:tomlj:$tomlj_version")
    include("org.antlr:antlr4-runtime:$antlr_version")
    localRuntime("org.tomlj:tomlj:$tomlj_version")

    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version"
    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$architectury_api_version"

    // REI
    modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:$rei_version"
//    // JEI is not available for fabric 1.21.4
//    compileOnly("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
//    localRuntime("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")
    // EMI: waiting for update to 1.21.4
//    compileOnly "dev.emi:emi-fabric:${emi_version}:api"
//    localRuntime "dev.emi:emi-fabric:${emi_version}"
    // No kubeJs as it no longer supports fabric after 1.20.1

    // TSS
    try {
        modCompileOnly("flat:toms_storage_fabric-1.21.4:2.3.2")
    } catch (Throwable t) {
        // If the dependency is not available, we can just ignore it.
        logger.warn("Toms Storage Fabric dependency not found, continuing without it.")
    }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}


base {
    archivesName = rootProject.archives_name + "-fabric"
}

modrinth {
    token = System.getenv("MODRINTH_MOD_PUBLISH")
    projectId = "croparia-if"
    versionName = archives_name + "-fabric-" + mod_version
    versionNumber = mod_version
    versionType = mod_version.endsWith("a") || mod_version.endsWith("alpha") ? "alpha" : mod_version.endsWith("pre") ? "beta" : "release"
    uploadFile = remapJar
    gameVersions = [minecraft_version]
    loaders = ["fabric"]
    File changelogFile = rootProject.file("changelog.md")
    if (changelogFile.exists()) {
        changelog = changelogFile.text
    }
    dependencies {
        required.project "architectury-api"
        required.project "fabric-api"
        incompatible.project "croparia"
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_MOD_PUBLISH")
    def mainFile = upload('971004', remapJar)
    mainFile.displayName = archives_name + "-fabric-" + mod_version
    mainFile.releaseType = mod_version.endsWith("a") || mod_version.endsWith("alpha") ? "alpha" : mod_version.endsWith("pre") ? "beta" : "release"
    File changelogFile = rootProject.file("changelog.md")
    if (changelogFile.exists()) {
        mainFile.changelog = changelogFile.text
    }
    mainFile.changelogType = 'markdown'
    mainFile.gameVersions = [minecraft_version]
    mainFile.addJavaVersion("Java 21")
    mainFile.addEnvironment("server", "client")
    mainFile.addModLoader("fabric")
    mainFile.addRequirement("architectury-api")
    mainFile.addRequirement("fabric-api")
    mainFile.addIncompatibility("croparia")
}